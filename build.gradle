/*
 * Copyright (C) 2011 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

task wrapper(type: Wrapper) {
  gradleVersion = '2.2.1'
}

buildscript {
    def rosMavenPath = "$System.env.ROS_MAVEN_PATH".split(':').collect { 'file://' + it }
    def rosMavenRepository = "$System.env.ROS_MAVEN_REPOSITORY"
    if ( rosMavenRepository == 'null' ) {
        rosMavenRepository = "https://github.com/rosjava/rosjava_mvn_repo/raw/master"
    }
    repositories {
        rosMavenPath.each { p ->
            maven {
                url p
            }
        }
        mavenLocal()
        maven {
            url rosMavenRepository
        }
    }
    dependencies {
        classpath 'org.ros.rosjava_bootstrap:gradle_plugins:[0.1,0.2)'
    }
}

/* Apply this here so we can share and avoid reconstructing generated package tree for each subproject */

apply plugin: 'catkin'
project.catkin.tree.generate()

allprojects {
    group 'org.ros.rosjava_messages'
    version = project.catkin.pkg.version
}

/* We do not want to build message packages for which we don't have sources in the local workspaces */
def nonEmptyProjects() {
    return subprojects.findAll { project.catkin.tree.pkgs.containsKey(it.name) }
}

configure(nonEmptyProjects()) {
    apply plugin: 'ros-java'
}

task distclean << {
    subprojects.each { p ->
       if ( p.name != 'rosjava_test_msgs' ) {
            delete p.name
        }
    }
}
